# Maintainer:  Peter Mattern <pmattern@arcor.de>
# Contributor: Yen Chi Hsuan <yan12125@gmail.com>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-panel
pkgname=$_pkgname-git
pkgver=0.16.1.r434.g0017a2f5
pkgrel=1
pkgdesc='The LXQt desktop panel'
arch=('i686' 'x86_64')
url='https://github.com/stefonarch/lxqt-panel/tree/wayland_panel'
license=("LGPL-2.1-only")
depends=('libstatgrab' 'libsysstat' 'libxtst' 'alsa-lib' 'libpulse'
         'layer-shell-qt' 'solid' 'kwindowsystem'
         'libdbusmenu-lxqt' 'liblxqt' 'lxqt-menu-data' 'lxqt-globalkeys')
makedepends=('git' 'cmake' 'qt6-tools' 'lxqt-build-tools')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/stefonarch/lxqt-panel#branch=wayland_panel")
sha256sums=('SKIP')

pkgver() {
      cd "$srcdir/$_pkgname"
      git describe --always | sed "s/-/.r/;s/-/./"
}

build() {
      mkdir -p build
      cd build
      cmake "$srcdir/$_pkgname" \
            -DCMAKE_INSTALL_PREFIX=/usr \
            -DCMAKE_INSTALL_LIBDIR=/usr/lib
      make
}

package() {
      cd build
      make DESTDIR="$pkgdir" install
}
